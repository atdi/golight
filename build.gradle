plugins {
    id 'java'
}

sourceCompatibility = 1.8

configurations {
    loader
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

repositories {
    mavenCentral()
}

dependencies {
    // Guice dependencies
    compile("com.google.inject:guice:$guiceVersion")
    compile("javax.inject:javax.inject:$javaxInjectVersion")
    // Jersey dependencies
    compile("org.glassfish.jersey.core:jersey-server:$jerseyVersion")
    compile("org.glassfish.hk2:guice-bridge:$guiceBridgeVersion")
    compile("org.glassfish.jersey.containers:jersey-container-servlet:$jerseyVersion")
    compile("org.glassfish.jersey.media:jersey-media-json-jackson:$jerseyVersion")
    // Embedded jetty dependencies
    compile("org.eclipse.jetty:jetty-server:$jettyVersion")
    compile("org.eclipse.jetty:jetty-servlet:$jettyVersion")
    // Spring boot loader dependency
    loader("org.springframework.boot:spring-boot-loader:$springBootLoaderVersion")
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task unpackLoader(type: Copy) {
    from {
        configurations.loader.collect {
            zipTree(it)
        }

    }
    include '**/*.class'
    into "$buildDir/classes/main/"

    includeEmptyDirs = false

}

jar {
    dependsOn unpackLoader
    into('lib') {
        from configurations.runtime
    }
    entryCompression org.gradle.api.tasks.bundling.ZipEntryCompression.STORED
    manifest {
        attributes("Main-Class": "org.springframework.boot.loader.JarLauncher",
                "Start-Class": "com.github.atdi.golight.Bootstrap")
    }
}